att omit_null_values
basetype time { mask "0hh'mm" }
basetype date { mask "!yyyymmdd|xxxx-xx-xx" }

definitions MainDefinitions {
	code
	DEFINE md_operatorId { "HTM" }
	DEFINE md_operatorName { "HTM" }
	end
}   

array_var av_used_gar			{ key_part 'gar_id' type 'boolean' assign 'false' }
array_var av_used_gar_stop		{ key_part 'stp_identifier' type 'boolean' assign 'false' }
array_var av_used_vehicle_group	{ key_part 'vehg_identifier' type 'boolean' assign 'false' }

file infrastructureframe {
	name '"NeTEx_InfrastructureFrame.xml"'

	xmlelement PublicationDelivery {
		xmlattribute xmlns { value '"http://www.netex.org.uk/netex"' }
		xmlattribute xmlns:gml { value '"http://www.opengis.net/gml/3.2"' }
		xmlattribute version { value '"1.0"' }
		
		xmlelement PublicationTimestamp { value 'DateToday()' }
		xmlelement ParticipantRef { value '"NDOV"' }
		
		xmlelement dataObjects {
			xmlelement CompositeFrame {			
				xmlelement FrameDefaults {
					xmlelement DefaultDataSourceRef {
						xmlattribute ref { value '"BISON:DataSource:" + md_operatorId' }
						xmlattribute version { value '"any"' }
					}
				}
			
				xmlelement frames {
					xmlelement ResourceFrame {
						xmlelement DataSource {
							xmlattribute id { value '"BISON:DataSource:" + md_operatorId' }
							xmlattribute version { value '"any"' }
							xmlelement ShortName { value 'md_operatorId' }
							xmlelement Description { value 'md_operatorName' }
						}
					}
				
					xmlelement InfrastructureFrame {
						xmlelement garages {
							
							# This segment selects the garages actually in use.
							foreach block {
								unique_on 'blk_gar_plc_strt, blk_gar_plc_end'
								foreach garage {
									from 'Object(blk_gar_plc_strt)'
									array_var av_used_gar		{ assign 'true' }
									
									foreach stop {
										from 'Object(gar_pull_in_stop)'
										array_var av_used_gar_stop { assign 'true' }
									}
									
									foreach stop {
										from 'Object(gar_pull_out_stop)'
										array_var av_used_gar_stop { assign 'true' }
									}
								
								} # foreach garage
								foreach garage {
									from 'Object(blk_gar_plc_end)'
									array_var av_used_gar		{ assign 'true' }
									
									foreach stop {
										from 'Object(gar_pull_in_stop)'
										array_var av_used_gar_stop { assign 'true' }
									}
									
									foreach stop {
										from 'Object(gar_pull_out_stop)'
										array_var av_used_gar_stop { assign 'true' }
									}
									
								} # foreach garage
							} # foreach block	
													
							foreach garage {		
								from 'GetAll(garage, av_used_gar)'
								unique_on 'gar_id'
																
								xmlelement Garage {
									xmlattribute id { value 'md_operatorId + ":Garage:" + gar_id' }
									xmlattribute version { value '"any"' }
									xmlattribute responsibilitySetRef { 
										condition 'gar_depot_id <> ""'
										value 'md_operatorId + ":ResponsibilitySet:" + gar_depot_id' 
									}
									xmlelement Name { value 'gar_description' }
									xmlelement members {			
										xmlelement GaragePointRef {						
											xmlattribute ref { value 'md_operatorId + ":GaragePoint:" + gar_pull_in_stop' }
											xmlattribute version { value '"any"' }					
										}
									
										xmlelement GaragePointRef {
											condition 'gar_pull_in_stop <> gar_pull_out_stop'
											xmlattribute ref { value 'md_operatorId + ":GaragePoint:" + gar_pull_out_stop' }
											xmlattribute version { value '"any"' }		
											
										}
									}
								}		
							}
						}					
						
						xmlelement vehicleAndCrewPoints {						
							foreach stop {
								from 'GetAll(stop, av_used_gar_stop)'
								unique_on 'stp_identifier'
								
								xmlelement GaragePoint { 
									xmlattribute id { value 'md_operatorId + ":GaragePoint:" + stp_identifier' }
									xmlattribute version { value '"any"' }
									xmlelement Name { value 'stp_description' }
									xmlelement ShortName { value 'stp_identifier' }
								}
							}
						}
						
						# At both HTM as Arriva vehicle types are being defined in Hastus
						# under vehicle groups, where vehicle types are left empty.
						
						xmlelement vehicleTypes {							
												
							# This segment selects the vehicleTypes actually in use.
							foreach block {
								unique_on 'vehg_identifier'
								foreach vehicle_group {
									from 'Object(vehg_identifier)'
									array_var av_used_vehicle_group	{ assign 'true' }
								}
							}
							
							foreach vehicle_group {
								from 'GetAll(vehicle_group, av_used_vehicle_group)'
								unique_on 'vehg_identifier'
														
								xmlelement VehicleType {
									xmlattribute id { value 'md_operatorId + ":VehicleType:" + vehg_identifier' }
									xmlattribute version { value '"any"' }
									xmlelement Name { value 'vehg_description' }
									xmlelement ShortName { value 'vehg_identifier' }
								}
							}
						}										
					}
				}
			}
		}
	}	
}